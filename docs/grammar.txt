program = statement list;

statement list = statement, {statement};
statement =
	library linking
	| procedure declaration
	| function declaration
	| string declaration
	| label declaration
	| assignment
	| jump
	| condition
	| function call;

library linking = "use", string, line break;
procedure declaration = "procedure", identifier, "/", number, line break;
function declaration = "function", identifier, "/", number, line break;
string declaration = "string", identifier, "=", string, line break;
label declaration = "label", identifier, line break;
assignment = "let", identifier, "=", expression, line break;
jump = "go", "to", identifier, line break;
condition =
	"if", expression, "then", line break,
		statement, line break;

expression = disjunction;
disjunction = conjunction, {"|", conjunction};
conjunction = equality, {"&", equality};
equality = comparison, {"=", comparison};
comparison = sum, {("<" | ">"), sum};
sum = product, {("+" | "-" | "$"), product};
product = unary, {("*" | "/"), unary};
unary = {"!"}, atom;

atom = number | identifier | function call | ("(", expression, ")");
function call = identifier, "(", [expression, {",", expression}], ")";

number = ? /\d+(\.\d+)?/ ?;
string = ? /"(\\.|[^"])*"/ ?;
identifier = ? /[a-z_]\w*/i ?;
line break = "\n";
